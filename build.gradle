plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '4.0.5'
    id 'com.epages.restdocs-api-spec' version '0.19.4'
}

group = 'com.sipomeokjo'
version = '0.0.1-SNAPSHOT'
description = ''

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.19.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}

openapi3 {
    server = 'http://localhost:8080'
    title = 'API'
    description = 'API description'
    version = '0.1.0'
    format = 'yaml'
}

// openapi3 결과를 build 쪽으로 복사
def swaggerOutDir = layout.buildDirectory.dir("generated-resources/swagger")

tasks.register("copyOasToSwagger", Copy) {
    dependsOn("openapi3")
    from(layout.buildDirectory.file("api-spec/openapi3.yaml"))
    into(swaggerOutDir)
}



/**
 * processResources는 swagger 디렉토리가 "있을 때만" 포함한다.
 * (없으면 그냥 넘어감)
 *
 * 그리고 Gradle validation을 위해 copyOasToSwagger 태스크를 inputs로 선언해서
 * "누가 만들어주는 output을 쓰는지" 관계를 명확하게 한다.
 */
tasks.named("processResources") {
    //inputs.files(tasks.named("copyOasToSwagger"))
    from(swaggerOutDir) {
        into("swagger")
    }
}

// 실행/패키징 시점에만 openapi 생성/복사
tasks.named("bootRun") {
    dependsOn("copyOasToSwagger")
}
tasks.named("bootJar") {
    dependsOn("copyOasToSwagger")
}
